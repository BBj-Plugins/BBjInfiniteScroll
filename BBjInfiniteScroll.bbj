REM /**
REM  * BBjInfiniteScrollPaginator.
REM  *
REM  * @author Hyyan Abo Fakher
REM  */
CLASS public BBjInfiniteScrollPaginator
  REM /**
  REM  * The total number of available items.
  REM  */
  FIELD public BBjNumber TotalItems! = 0
  REM /**
  REM  * The current active page
  REM  */
  FIELD public BBjNumber CurrentPage! = 1
  REM /**
  REM  * The number of items per page
  REM  */
  FIELD public BBjNumber PageSize! = 10
  FIELD protected BBjNumber TotalPages! = 0
  FIELD protected BBjNumber StartIndex! = 0
  FIELD protected BBjNumber EndIndex! = 0
  REM /**
  REM  * Construct a new BBjInfiniteScrollPaginator
  REM  */
  METHOD public BBjInfiniteScrollPaginator()
    #calculate()
  METHODEND
  REM /**
  REM  * Set the total number of available items
  REM  *
  REM  * @param value! The value
  REM  */
  METHOD public void setTotalItems(BBjNumber value!)
    #TotalItems! = value!
    #calculate()
  METHODEND
  REM /**
  REM  * Set the current active page
  REM  *
  REM  * @param value! The value
  REM  */
  METHOD public void setCurrentPage(BBjNumber value!)
    #CurrentPage! = value!
    #calculate()
  METHODEND
  REM /**
  REM  * Set the number of items per page
  REM  *
  REM  * @param value! The value
  REM  */
  METHOD public void setPageSize(BBjNumber value!)
    #PageSize! = value!
    #calculate()
  METHODEND
  REM /**
  REM  * Get the number of total pages
  REM  *
  REM  * @return The total pages
  REM  */
  METHOD public BBjNumber getTotalPages()
    METHODRET #TotalPages!
  METHODEND
  REM /**
  REM  * Get the index of first item
  REM  *
  REM  * @return The index
  REM  */
  METHOD public BBjNumber getStartIndex()
    METHODRET #StartIndex!
  METHODEND
  REM /**
  REM  * Get the index of last item
  REM  *
  REM  * @return The index
  REM  */
  METHOD public BBjNumber getEndIndex()
    METHODRET #EndIndex!
  METHODEND

  METHOD protected void calculate()
    #TotalPages! = java.lang.Math.ceil(#TotalItems! / #PageSize!)

    IF(#CurrentPage! < 1) THEN
      #CurrentPage! = 1
    ELSE
      IF(#CurrentPage! > #TotalPages!)
        #CurrentPage! = #TotalPages!
      FI
    FI

    #StartIndex! = (#CurrentPage! - 1) * #PageSize!
    #EndIndex! = java.lang.Math.min(#StartIndex! + #PageSize! - 1, #TotalItems! - 1)
  METHODEND

CLASSEND
REM /**
REM  * An event which is fired when the user scrolls to the bottom of the
REM  * container.
REM  *
REM  * @author Hyyan Abo Fakher
REM  */
CLASS public BBjInfiniteScrollEvent

  FIELD private BBjInfiniteScroll Component!
  REM /**
  REM  * Construct a new BBjInfiniteScrollEvent
  REM  *
  REM  * @param component! The instance of the BBjInfiniteScroll object
  REM  */
  METHOD public BBjInfiniteScrollEvent(BBjInfiniteScroll component!)
    #Component! = component!
  METHODEND
  REM /**
  REM  * Get the BBjInfiniteScroll instance
  REM  *
  REM  * @return The widget instance
  REM  */
  METHOD public BBjInfiniteScroll getWidget()
    METHODRET #Component!
  METHODEND
  REM /**
  REM  * Alias FOR `BBjInfiniteScrollEvent.getWidget()`
  REM  *
  REM  * @return The control instance
  REM  */
  METHOD public BBjInfiniteScroll getControl()
    METHODRET #getWidget()
  METHODEND

CLASSEND
REM /**
REM  * BBjInfiniteScroll lets a user loads content continuously as the user scrolls down, eliminating the need FOR pagination.
REM  *
REM  * Infinite scrolling is a technique that loads more content as you scroll.
REM  * It allows you to CONTINUE scrolling indefinitely AND is sometimes known as endless scrolling.
REM  * The technique is often used in social media feeds.
REM  *
REM  * @author Hyyan Abo Fakher
REM  */
CLASS public BBjInfiniteScroll
  REM /**
  REM  * The refresh icon to USE when loading more data.
  REM  */
  FIELD public BBjString Icon! = "bbj:animated-spinner"
  REM /**
  REM  * The instructions string.
  REM  */
  FIELD public BBjString Text! = "Loading data"
  REM /**
  REM  * When true , the infinite scroll is completed AND there are no more data.
  REM  * Setting this FIELD won't update the widget automatically, you will need to manually
  REM  * invoke `BBjInfiniteScroll:update`
  REM  */
  FIELD public BBjNumber Completed! = 0
  REM /**
  REM  * The canvas which will be used to render the data.
  REM  */
  FIELD private BBjChildWindow Canvas!
  FIELD private BBjWebComponent WebComponent!
  REM /**
  REM  * Construct a new BBjInfiniteScroll component
  REM  *
  REM  * @param wnd! The window instance
  REM  */
  METHOD public BBjInfiniteScroll(BBjWindow wnd!)
    #WebComponent! = wnd!.addWebComponent("dwc-infinite-scroll")
    #WebComponent!.setStyle("width","")
    #WebComponent!.setStyle("height","")
    #WebComponent!.setStyle("position","")
    #WebComponent!.setTabTraversable(0)
    #WebComponent!.setFocusable(0)
    #Canvas! = wnd!.addChildWindow(32766, 0, 0, 0, 0, "", $00008810$, BBjAPI().getSysGui().getAvailableContext())
    #Canvas!.setAttribute("bbj-remove", "true")
    #WebComponent!.setSlot(#Canvas!)
    handler! = new BBjInfiniteScrollEventHandler(#this!)
    #WebComponent!.setCallback("dwc-infinite", handler!, "handleClientEvents")
  METHODEND
  REM /**
  REM  * Get the widget's id
  REM  *
  REM  * @return BBjNumber
  REM  */
  METHOD public BBjNumber getID()
    METHODRET #getWebComponent().getID()
  METHODEND
  REM /**
  REM  * Get the instance of the component's Canvas
  REM  *
  REM  * @return The canvas's instance
  REM  */
  METHOD public BBjChildWindow getCanvas()
    METHODRET #Canvas!
  METHODEND
  REM /**
  REM  * Set the refresh icon to USE when loading more data.
  REM  *
  REM  * Icons can in any of the following formats:
  REM  *
  REM  * <ol>
  REM  *  <li> <b>URL</b>: (ex: /path/to/image.png)
  REM  *  <li> <b>Data URL</b>: (ex: data:image/jpeg;base64,/9j/4SDpRXhpZgAAT....)
  REM  *  <li> <b>ICON_NAME</b>: An icon to LOAD FROM the default BBj icons pool. (ex: `arrow`)
  REM  *  <li> <b>POOL_NAME:ICON_NAME</b>: An icon to LOAD FROM the passed pool. (ex: `feather:arrow`)
  REM  * </ol>
  REM  *
  REM  * @param icon! The icon BBjString
  REM  */
  METHOD public void setIcon(BBjString icon!)
    #Icon! = icon!
    #getWebComponent().putClientProperty("icon", icon!)
  METHODEND
  REM /**
  REM  * Set the instructions string.
  REM  *
  REM  * @param value! The pull text
  REM  */
  METHOD public void setText(BBjString value!)
    #Text! = value!
    #getWebComponent().putClientProperty("text", value!)
  METHODEND
  REM /**
  REM  * Add a widget attribute.
  REM  *
  REM  * @param key! The attribute's name
  REM  * @param value! The attribute's value
  REM  */
  METHOD public void setAttribute(BBjString key!, BBjString value!)
    #getWebComponent().setAttribute(key!, value!)
  METHODEND
  REM /**
  REM  * Get a widget attribute.
  REM  *
  REM  * @param key! The attribute's name
  REM  */
  METHOD public BBjString getAttribute(BBjString key!)
    METHODRET #getWebComponent().getAttribute(key!)
  METHODEND
  REM /**
  REM  * Add a css CLASS to the widget.
  REM  *
  REM  * @param class! The css CLASS to add
  REM  */
  METHOD public void addClass(BBjString class!)
    #getWebComponent().addClass(class!)
  METHODEND
  REM /**
  REM  * REMOVE a css CLASS to the widget.
  REM  *
  REM  * @param class! The css CLASS to add
  REM  */
  METHOD public void removeClass(BBjString class!)
    #getWebComponent().removeClass(class!)
  METHODEND
  REM /**
  REM  * Update the canvas
  REM  *
  REM  * Every time the canvas is changed in BBj , this METHOD should be called to
  REM  * update the client view
  REM  */
  METHOD public void update()
    script! = "" +
:    "(async () => {" +
:    " await customElements.whenDefined('dwc-infinite-scroll');" +
:    " component.completed = Boolean(" + str(#getCompleted()) + ");"+
:    " await component.update()" +
:    "})()"
    #getWebComponent().executeAsyncScript(script!)
  METHODEND
  REM /**
  REM  * Set a scroll CALLBACK
  REM  *
  REM  * The CALLBACK will be invoked every time the user asks FOR more data by scrolling
  REM  *
  REM  * @param callback! The event CALLBACK
  REM  */
  METHOD public void onScroll(String callback!)
    BBjAPI().setCustomEventCallback(str(#this!) + "dwc-infinite", callback!)
  METHODEND
  REM /**
  REM  * Set a scroll CALLBACK
  REM  *
  REM  * The CALLBACK will be invoked every time the user asks FOR more data by scrolling
  REM  *
  REM  * @param callback! A CLASS instance
  REM  * @param callback! The event METHOD
  REM  */
  METHOD public void onScroll(CustomObject instance!, BBjString method!)
    BBjAPI().setCustomEventCallback(str(#this!) + "dwc-infinite", instance!, method!)
  METHODEND
  REM /**
  REM  * CLEAR the scroll CALLBACK
  REM  */
  METHOD public void clearScrollCallback()
    BBjAPI().clearCustomEventCallback(str(#this!) + "dwc-infinite")
  METHODEND

CLASSEND
REM /**
REM  * A CLASS which is used to handle the dwc-infinite-scroll client events.
REM  *
REM  * @author Hyyan Abo Fakher
REM  */
CLASS public BBjInfiniteScrollEventHandler

  FIELD private BBjInfiniteScroll Widget!

  METHOD public BBjInfiniteScrollEventHandler(BBjInfiniteScroll widget!)
    #Widget! = widget!
  METHODEND
  REM /**
  REM  * Handle the client events
  REM  *
  REM  * @param ev! The event
  REM  */
  METHOD public void handleClientEvents(BBjWebEvent ev!)
    type$ = ev!.getEventType()
    payload! = new BBjInfiniteScrollEvent(#Widget!)

    IF(type$ = "dwc-infinite") THEN
      BBjAPI().postPriorityCustomEvent(str(#Widget!) + "dwc-infinite", payload!)
    FI
  METHODEND

CLASSEND
